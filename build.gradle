plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.4.0'
    id 'groovy'
    id 'idea'
}

defaultTasks 'clean', 'build'

sourceCompatibility = 1.7
ext.rundeckPluginVersion = '0.1'

/**
 * Set to a comma-separated list of full classnames of Rundeck plugins.
 */
ext.pluginClassNames='org.rundeck.plugin.scm.p4.P4ExportPluginFactory,org.rundeck.plugin.scm.p4.P4ImportPluginFactory'
ext.pluginName = 'Perforce Plugin'
ext.pluginDescription = 'Export and Import Jobs with a Perforce repository'

scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
        // append .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += '.0'
            }
            orig
        }
    }
}
project.version = scmVersion.version

configurations{
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile {
        extendsFrom pluginLibs
    }
}
repositories {
    mavenLocal()
    mavenCentral()

    maven { 
        // snapshot repo for rundeck-core SNAPSHOT
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    pluginLibs('com.perforce:p4java:2015.2.1365273')

    compile group: 'org.rundeck', name: 'rundeck-core', version: '2.6.9'

    compile 'org.codehaus.groovy:groovy-all:2.3.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.6.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.6.2'
    compile group: 'com.perforce', name: 'p4java', version: '2015.2.1365273'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'com.googlecode.java-diff-utils', name: 'diffutils', version: '1.3.0'

    testCompile 'junit:junit:4.8.1',
                'org.mockito:mockito-all:1.8.5'

    testCompile 'org.codehaus.groovy:groovy-all:2.3.7'
    testCompile 'cglib:cglib-nodep:2.2.2'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}


// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-Name': pluginName
        attributes 'Rundeck-Plugin-Description': pluginDescription
        def libList = configurations.pluginLibs.collect { 'lib/' + it.name }.join(' ')
        attributes 'Rundeck-Plugin-Libs': "${libList}"
        attributes 'Rundeck-Plugin-Archive': 'true'
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)


apply plugin: 'maven'
task createPom << {
    pom {
        project {
            artifactId project.name
            groupId "org.rundeck.plugins"
            inceptionYear '2016'
            packaging 'jar'
            version version
            name( 'Rundeck '+pluginName)
            url 'http://rundeck.org'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            parent {
                groupId 'com.dtolabs.rundeck'
                artifactId "rundeck-bundled-plugins"
                version(version)
            }
            build {
                plugins{
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-jar-plugin'
                        version '2.3.2'
                        configuration{
                            archive{
                                manifestEntries{
                                    'Rundeck-Plugin-Classnames'(pluginClassNames)
                                    'Rundeck-Plugin-File-Version' version
                                    'Rundeck-Plugin-Version' rundeckPluginVersion
                                    'Rundeck-Plugin-Archive' 'true'
                                    'Rundeck-Plugin-Name' pluginName
                                    'Rundeck-Plugin-Description' pluginDescription
                                }
                            }
                        }
                    }
                }
            }
        }
    }.writeTo("pom.xml")
}

